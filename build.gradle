buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath "com.github.GTNH2:ForgeGradle:FG_1.2-SNAPSHOT"
    }
}

apply plugin: 'forge'

minecraft {
	version = minecraft_version + "-" + forge_version
}

version = "${minecraft_version}-${tictooltips_version}"
group = project.projectDir.name.toLowerCase()
archivesBaseName = project.projectDir.name

sourceSets.main{
	java {
		srcDirs 'src/main/java', 'apis' //set the source folder as the /src subfolder
	}
	resources {
		srcDirs 'src/main/resources' //set the resources folder as the /resources subfolder
	}
}

repositories {
	maven {
		name "TiC"
		url "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name "NEI"
		url "http://chickenbones.net/maven/"
	}
}

dependencies {
	compile "tconstruct:TConstruct:${project.minecraft.version}-${tcon_version}:deobf"
}

// variable substitution in java files
task processSourceMainJava(type:Copy) {
	inputs.property "vars", project.version + project.minecraft.version
	from('java')
	{
		include '**/ModInfo.java'
		include '**/package-info.java'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	into project.buildDir.getPath()+"/sources/java"
}

// remove apis only necessary for compilation from the build
task removeExternalAPIs(type:Delete) {
	delete += sourceSets.main.output.classesDir.getPath()+"/cofh"
}

// correct task ordering
afterEvaluate { project ->
	sourceMainJava.finalizedBy(processSourceMainJava)
	compileJava.finalizedBy(removeExternalAPIs)
}

processResources {
	inputs.property "vars", project.version + project.minecraft.version
	from(sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
	}
}